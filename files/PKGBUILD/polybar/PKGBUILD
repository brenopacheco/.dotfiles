pkgname="polybar"
pkgver=3.6.3
pkgrel=1
pkgdesc="A fast and easy-to-use status bar"
arch=("x86_64")
url="https://polybar.github.io/"
license=("MIT")
depends=("libuv" "cairo" "xcb-util-image" "xcb-util-wm" "xcb-util-xrm"
	"xcb-util-cursor" "alsa-lib" "libpulse" "libmpdclient" "libnl"
	"jsoncpp" "curl")
makedepends=("cmake" "git" "python" "pkg-config" "python-sphinx" "python-packaging" "i3-wm")
provides=("polybar")
conflicts=("polybar")
source=("git+https://github.com/polybar/polybar#commit=b754790642a49d9379d08e9e03ee22e417e12eda")
sha256sums=("SKIP")

prepare() {
	git -C "polybar" submodule update --init --recursive
	mkdir -p "polybar/build"
}

build() {
	cd "polybar/build" || exit 1
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DPYTHON_EXECUTABLE=/usr/bin/python3 ..
	cmake --build .
}

package() {
	cmake --build "polybar/build" --target install -- DESTDIR="${pkgdir}"
	install -Dm644 "polybar/LICENSE" "${pkgdir}/usr/share/licenses/polybar/LICENSE"
}
