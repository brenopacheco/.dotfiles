# shellcheck disable=all

dmenu=dmenu
pkgname="$dmenu-fork"
pkgver=5.2.r10.b54bdab
pkgrel=1
pkgdesc="Generic menu for X"
arch=(x86_64)
url="https://github.com/brenopacheco/dmenu-fork"
license=(MIT)
depends=(
	coreutils
	fontconfig
	freetype2
	glibc
	libfontconfig.so
	libx11
	libxft
	libxinerama
	sh
)
makedepends=(git)
provides=("dmenu")
conflicts=("dmenu")
source=("git+https://github.com/brenopacheco/dmenu-fork")
b2sums=('SKIP')

pkgver() {
	cd dmenu-fork
	printf "5.2.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build(){
	cd dmenu-fork
	make X11INC=/usr/include/X11 X11LIB=/usr/lib
}

package() {
	cd dmenu-fork
	make PREFIX=/usr DESTDIR="$pkgdir" install
	install -vDm644 LICENSE -t "$pkgdir/usr/share/licenses/$dmenu/"
}
