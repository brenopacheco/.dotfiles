pkgname=(eww-x11)
pkgver=0.4.0
pkgrel=3
pkgdesc="ElKowar's wacky widgets"
url='https://elkowar.github.io/'
arch=(x86_64)
license=(MIT)
makedepends=(rustup git)
depends=(gtk3)
provides=(eww)
conflicts=(eww)

source=("git+https://github.com/elkowar/eww#commit=d58d91de78d2b9984a1bbb8314be197bc0e283dc")
b2sums=('SKIP')

prepare() {
	NIGHTLY=$(rustc  --version | grep "nightly")
	if [[ -z "$NIGHTLY" ]]; then
		echo "Rust toolchain needs to be nightly" >&2
		echo "Run: rustup default nightly" >&2
		exit 1
	fi
	cd eww
	cargo update
	cargo fetch --locked
}

build() {
	cd eww
	cargo build --frozen --release --no-default-features --features x11
}

package() {
	cd eww
	install -vDm644 LICENSE -t "$pkgdir/usr/share/licenses/eww/"
	install -vd "$pkgdir/etc/xdg/eww/"
	cp -vr examples/eww-bar "$pkgdir/etc/xdg/eww/"
	install -vDm755 target/release/eww -t "$pkgdir/usr/bin/"
}

