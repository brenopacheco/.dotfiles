#!/usr/bin/env perl

use Getopt::Long qw(GetOptions);
use Pod::Usage   qw(pod2usage);
use builtin      qw(trim);

use feature 'say';

my $help = 0;
my $man  = 0;
my $list = 0;
GetOptions(
    'h|help' => \$help,
    'l|list' => \$list,
    'man'    => \$man,
) or pod2usage(2);
pod2usage(1)               if $help;
pod2usage( -verbose => 2 ) if $man;

my $dir = "$ENV{'HOME'}/.screenlayout";
opendir( my $dh, $dir ) or die "Can't open directory '$dir': $!";
my @files = sort grep { !/^\./ && -x "$dir/$_" } readdir($dh);
closedir($dh);

my @profiles = ();
for my $file (@files) {
    my $name = trim($1)
      if `grep -e "#\\s*name\\s*=" -m1 $dir/$file` =~ /name\s*=(.+)/;
    my $desc = trim($1)
      if `grep -e "#\\s*desc\\s*=" -m1 $dir/$file` =~ /desc\s*=(.+)/;
    push @profiles,
      {
        path => "$dir/$file",
        name => $name // $file,
        desc => $desc // ""
      };
}

if ($list) {
    my $mlen =
      ( sort { $b <=> $a } map { length( $_->{name} ) } @profiles )[0] + 3;
    say "Available profiles:";
    for my $profile (@profiles) {
        printf "  %-${mlen}s %s\n", $profile->{name}, $profile->{desc};

    }
    exit 0;
}

my $profile = shift @ARGV;

pod2usage() unless ($profile);

my @matches = map {
    $profile eq $_->{name} ? $_->{path} : ()
} @profiles;

if (!@matches) {
    pod2usage("No profile matches '$profile'");
}

if (length (@matches) > 1) {
    pod2usage("Multiple profiles match '$profile'");
}

system(@matches[0]);

__END__

=head1 NAME

monitor - apply screenlayout X11 profiles

=head1 SYNOPSIS

    monitor [OPTION]... [PROFILE]

=head1 OPTIONS

=over 4

=item B<-h, --help>

Show usage

=item B<--man>

Read docs in man pager

=item B<-l, --list>

List available profiles

=back

=head1 DESCRIPTION

B<This program> applies available X11 profile configurations from ~/.screenlayout.
These profiles are B<executable> scripts containing the appropriate B<xrandr> commands.

When a PROFILE is given, the program applies it, unless the --list option is
passed. Profiles are shell scripts in ~/.screenlayout with comment key-value
pairs:

  # name = profile name
  # desc = profile description

=head1 AUTHOR

Breno Pacheco <github.com/brenopacheco>

=cut
